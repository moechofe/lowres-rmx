'CENTERING
GLOBAL TOP,LEF
TOP=(384-SHOWN.H)\2
LEF=(216-SHOWN.W)\2

'COLOR INDEX 0..63
GLOBAL C
C=1

'FOR BLACK SEPARATOR
PALETTE 2,0,0,0,0

FOR I=0 to 64-1
'COLOR INDEX
BG 0
PAL 1
Y=I\4*2+8
X=(I*6)MOD (4*6)+2
TEXT X,Y,STR$(I)
'BLACK SEPARATOR
PAL 2
CELL X-1,Y,1
CELL X-1,Y+1,1
CELL X+5,Y,1
CELL X+5,Y+1,1
'COLORED BOX
BG 1
PAL 0
BG FILL X,Y TO X+5,Y+1 CHAR (I MOD 4)
NEXT I

'BLACK BORDER ON THE SIDE
BG 0
PAL 2
BG FILL 0,0 TO 0,47 CHAR 1
BG FILL 26,0 to 26,47 CHAR 1

ON RASTER CALL PALETTECYCLE
SUB PALETTECYCLE
IF RASTER MOD 16<>0 THEN EXIT SUB
'RESET WHEN RASTER IS OOB
IF RASTER<64 OR RASTER>=384-64 THEN
PALETTE 0,0,0,0,0
C=0
EXIT SUB
END IF
'CYCLE COLOR OF BOX
PALETTE 0,C,C+1,C+2,C+3
ADD C,4,0 TO 60
END SUB

SCROLL 0,LEF,TOP
SCROLL 1,LEF,TOP

DO
WAIT VBL
LOOP

#2:MAIN CHARACTERS
00000000000000000000000000000000
FFFFFFFFFFFFFFFF0000000000000000
0000000000000000FFFFFFFFFFFFFFFF
FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF
